%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Description:   Lightweight Java (LJ)'s most basic definitions.              %
% Author:        Rok Strnisa <rok@strnisa.com>, 2007-2009                     %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

indexvar j, k, l ::=  {{ com index variable }} {{ isa nat }}
metavar Field {{ isa f }}, f ::=  {{ com field name }} {{ isa string }}
metavar Method {{ isa meth }}, meth ::=  {{ com method name }} {{ isa string }}
metavar Var {{ isa var }}, var ::=  {{ com normal variable }} {{ isa string }}
metavar dcl ::=  {{ com name of derived class }} {{ isa string }}
metavar Pointer {{ isa oid }}, oid ::=  {{ com object identifier }} {{ isa nat }}

grammar
%%% TERMINALS %%%
  terminals :: termi_ ::=
   |  (      :: :: lround   {{ tex (\! }} {{ com left expression bracket }}
   |  )      :: :: rround   {{ tex \!) }} {{ com right expression bracket }}
   |  [      :: :: lsquare  {{ tex [\! }} {{ com left environment bracket }}
   |  ]      :: :: rsquare  {{ tex \!] }} {{ com right environment bracket }}
   |  []     :: :: empty    {{ tex [\,] }} {{ com empty list }}
   |  ,      :: :: comma    {{ tex \!, }} {{ com separator }}
   |  /      :: :: replace  {{ tex / }} {{ com `replaces' }}
   |  :      :: :: colon    {{ tex \!\!:\!\! }} {{ com `is of type' }}
   |  >      :: :: gt       {{ tex \!>\! }} {{ com `is greater than' }}
   |  <      :: :: subty    {{ tex \!\!\prec\! }} {{ com `is a subtype of' }}
   |  =<     :: :: le       {{ tex \!\le\! }} {{ com `is less than or equal to' }}
   |  ->     :: :: returns  {{ tex \rightarrow }} {{ com `returns' }}
   |  |->    :: :: mapsto   {{ tex \mapsto }} {{ com `maps to' }} {{ isa \<mapsto> }}
   |  -->    :: :: reduce   {{ tex \longrightarrow }} {{ com `implies' or `reduces' }}
   |  =>     :: :: results  {{ tex \Rightarrow }} {{ com `results in' }}
   |  =      :: :: alias    {{ tex \!=\! }} {{ com equality or alias }}
   |  !=     :: :: neq      {{ tex \neq }} {{ com `is not equal to' }}
   |  |-     :: :: vdash    {{ tex \vdash }} {{ com `can deduce' }}
   |  ALL    :: :: all      {{ tex \forall\! }} {{ com `for all' }}
   |  EX     :: :: ex       {{ tex \exists\! }} {{ com `there exists' }}
   |  in     :: :: in       {{ tex \!\in\! }} {{ com `in' }}
   |  notin  :: :: notin    {{ tex \!\notin\! }} {{ com `not in' }}
   |  \/     :: :: or       {{ tex \,\vee\, }} {{ com `or' }}
   |  /\     :: :: and      {{ tex \,\wedge\, }} {{ com `and' }}
   |  bot    :: :: bot      {{ tex \bot }} {{ com `is disjoint from' or `bottom' }}
   |  ~>     :: :: trans    {{ tex \leadsto }} {{ com `is translated to' }}

%%% NN %%%
  nn :: nn_ ::=  {{ com natural number (nat) }} {{ isa nat }}
   |  0               :: M :: zero  {{ com zero }} {{ isa 0 }}
   |  1               :: M :: one   {{ com one }} {{ isa 1 }}
   |  nn + nn'        :: M :: plus  {{ com plus }} {{ isa ([[nn]]+[[nn']]) }}

%%% VARIABLE %%%
  TVar {{ isa x }}, x , y :: x_ ::=  {{ com term variable }}
   |  var      ::   :: var   {{ com normal variable }}
   |  this     ::   :: this  {{ com ref.~to current object }}

%%% VARIABLES %%%
  X {{ tex \overline{x} }} , Y {{ tex \overline{y} }} :: xs_ ::=
           {{ com term variables ($\mathit{x}$ list) }} {{ isa x list }}
   |  x1 .. xk  :: M :: def  {{ com def }} {{ isa [[x1..xk]] }}

%%% CLASS NAME %%%
  C {{ isa cl }}, cl :: cl_ ::=  {{ com class name }}
   |  Object  :: :: object  {{ com top class }} {{ tex {\tt Object} }}
   |  fqn     :: :: fqn     {{ com fully qualified name }}

%%% FIELD DECLARATION %%%
  fd :: fd_ ::=  {{ com field declaration }}
   |  cl f ; :: :: def  {{ com def. }}

%%% FIELD DECLARATIONS %%%
  fds {{ tex \mathit{\overline{fd} } }} :: fds_ ::=
           {{ com field declarations ($\mathit{fd}$ list) }} {{ isa fd list }}
   |  []          :: M :: empty  {{ com empty }} {{ isa [] }}
   |  fd1 .. fdk  :: M :: def    {{ com def. }} {{ isa [[fd1..fdk]] }}

%%% FIELDS %%%
  fs {{ tex \mathit{\overline{f} } }} :: fs_ ::=
           {{ com fields ($\mathit{f}$ list) }} {{ isa f list }}
   |  []        :: M :: empty   {{ com empty }} {{ isa [] }}
   |  f1 .. fk  :: M :: def     {{ com def. }} {{ isa [[f1 .. fk]] }}
   |  fs ; fs'  :: M :: append  {{ com append }} {{ isa ([[fs]]@[[fs']]) }}

%%% FIELDS OPTION %%%
  fs_opt {{ tex \mathit{\overline{f}_{opt} } }} :: fs_opt_ ::=
           {{ com fields option ($\overline{\mathit{f} }$ option) }} {{ isa fs option }}
   |  null  :: M :: none  {{ com none }} {{ isa None }}
   |  fs    :: M :: some  {{ com some }} {{ isa Some ([[fs]]) }}

%%% STATEMENT %%%
  s :: s_ ::=  {{ com statement }}
   |  { </ sk // k /> }         :: :: block  {{ com block }}
   |  var = x ;                 :: :: ass    {{ com variable assignment }}
   |  var = x . f ;             :: :: read   {{ com field read }}
   |  x . f = y ;               :: :: write  {{ com field write }}
   |  if ( x == y ) s else s'   :: :: if     {{ com conditional branch }}
   |  var = new ctx cl ();      :: :: new
           {{ com object creation }}
           {{ tex [[var]]=[[new]]_{[[ctx]]}[[cl]](); }}
   |  var = x . meth ( Y ) ;    :: :: call
           {{ com method call }}

%%% VARIABLE DECLARATION %%%
  vd :: vd_ ::=  {{ com variable declaration }}
   |  cl var  :: :: def  {{ com def. }}

%%% VARIABLE DECLARATIONS %%%
  vds {{ tex \overline{vd} }} :: vds_ ::=
           {{ com variable declarations ($\mathit{vd}$ list) }} {{ isa vd list }}
   |  vd1 .. vdk  :: M :: def  {{ com def. }} {{ isa [[vd1..vdk]] }}

%%% METHOD SIGNATURE %%%
  meth_sig :: meth_sig_ ::=  {{ com method signature }}
   |  cl meth ( vds )  :: :: def  {{ com def. }}

%%% METHOD BODY %%%
  meth_body :: meth_body_ ::=  {{ com method body }}
   |  s1 .. sk return y ;  :: :: def  {{ com def. }}

%%% METHOD DEFINITION %%%
  meth_def :: meth_def_ ::=  {{ com method definition }}
   |  meth_sig { meth_body }  :: :: def  {{ com def. }}

%%% METHOD DEFINITIONS %%%
  meth_defs {{ tex \mathit{\overline{meth\_def} } }} :: meth_defs_ ::=
           {{ com method def.'s ($\mathit{meth\_def}$ list) }} {{ isa meth_def list }}
   |  []                      :: M :: empty  {{ com empty }} {{ isa [] }}
   |  meth_def1 .. meth_defk  :: M :: def
           {{ com def. }} {{ isa [[meth_def1..meth_defk]] }}

%%% METHOD DEFINITION OPTION %%%
  meth_def_opt {{ tex \mathit{meth\_def_{opt} } }} :: meth_def_opt_ ::=
           {{ com method def.~option ($\mathit{meth\_def}$ option) }} {{ isa meth_def option }}
   |  null      :: M :: none  {{ com none }} {{ isa None }}
   |  meth_def  :: M :: some  {{ com some }} {{ isa Some ([[meth_def]]) }}

%%% METHOD DEFINITION IN CONTEXT OPTION %%%
  ctxmeth_def_opt {{ tex \mathit{ctxmeth\_def_{opt} } }} :: ctxmeth_def_opt_ ::=
           {{ com method def.~in context option ($(\mathit{ctx} \times \mathit{meth\_def})$ option) }}
           {{ isa (ctx \<times> meth_def) option }}
   |  null                :: M :: none
           {{ com none }} {{ isa (None::ctxmeth_def_opt) }}
   |  ( ctx , meth_def )  :: M :: some
           {{ com some }} {{ isa (Some ([[ctx]],[[meth_def]])::ctxmeth_def_opt) }}

%%% METHOD NAMES %%%
  meths {{ tex \overline{meth} }} :: meths_ ::=
           {{ com method names ($\mathit{meth}$ list) }} {{ isa meth list }}
   |  []              :: M :: empty  {{ com empty }} {{ isa [] }}
   |  meth1 .. methk  :: M :: def
           {{ com def. }} {{ isa [[meth1..methk]] }}
   |  meths ; meths'  :: M :: append
           {{ com append }} {{ isa ([[meths]]@[[meths']]) }}

%%% CLASS DEFINITIONS %%%
  clds {{ tex \overline{cld} }} :: clds_ ::=
           {{ com class def.'s ($\mathit{cld}$ list) }} {{ isa cld list }}
   |  []            :: M :: empty  {{ com empty }} {{ isa [] }}
   |  cld1 .. cldk  :: M :: def    {{ com def. }} {{ isa [[cld1..cldk]] }}

%%% TYPE %%%
  Type {{ isa ty }} , ty {{ tex \tau }} :: ty_ ::=  {{ com type }}
   |  Object     :: :: top  {{ com supertype of all types }}
           {{ tex {\tt Object} }}
   |  ctx . dcl  :: :: def  {{ com class identifier }}
           {{ tex [[ctx]]\hspace{-.1ex}.\hspace{-.1ex}[[dcl]] }}

%%% TYPE OPTION %%%
  ty_opt {{ tex { \tau_{opt} } }} :: ty_opt_ ::=
           {{ com result of type lookup ($\tau$ option) }} {{ isa ty option }}
   |  null       :: M :: none   {{ com none }} {{ isa None }}
   |  ty         :: M :: some
          {{ com some }} {{ isa (Some ([[ty]])) }}
   |  G ( x )    :: M :: tyenv
          {{ com static type lookup }} {{ isa [[G]] [[x]] }}
   |  H ( oid )  :: M :: heap
           {{ com dynamic type lookup }}
           {{ isa (case [[H]] [[oid]] of
                     None \<Rightarrow> None
                   | Some tyfs \<Rightarrow> Some (fst tyfs)) }}

%%% TYPE OPTION (WITH BOTTOM) %%%
  ty_opt_bot {{ tex { \tau_{opt}^{\bot} } }} :: ty_opt_bot_ ::=
           {{ com result of type lookup that can abort }}
   |  ty_opt  :: :: opt  {{ com result of type lookup }}
   |  bot     :: :: bot  {{ com failed to find a type }}

%%% CLASS DEFINITION IN CONTEXT %%%
  ctxcld :: ctxcld_ ::=
           {{ com class def.~in context ($\mathit{ctx} \times \mathit{cld}$) }} {{ isa (ctx \<times> cld) }}
   |  ( ctx , cld )  :: M :: def  {{ com def. }} {{ isa ([[ctx]],[[cld]]) }}

%%% CLASS DEFINITIONS IN CONTEXT %%%
  ctxclds {{ tex \overline{ctxcld} }} :: ctxclds_ ::=
           {{ com class def.'s~in context ($\mathit{ctxcld}$ list) }} {{ isa ctxcld list }}
   |  []                    :: M :: empty  {{ com empty }} {{ isa [] }}
   |  ctxcld1 .. ctxcldk    :: M :: def
           {{ com def. }} {{ isa [[ctxcld1..ctxcldk]] }}
   |  ctxclds @ [ ctxcld ]  :: M :: rev_cons
           {{ com rev cons }} {{ isa [[ctxclds]]@[ [[ctxcld]] ] }}

%%% CLASS DEFINITION LOOKUP RESULT %%%
  ctxcld_opt {{ tex ctxcld_{opt} }} :: ctxcld_opt_ ::=
           {{ com class def.~lookup result ($\mathit{ctxcld}$ option) }} {{ isa ctxcld option }}
   |  null    :: M :: none  {{ com none }} {{ isa None }}
   |  ctxcld  :: M :: some  {{ com some }} {{ isa (Some ([[ctxcld]])) }}

%%% CLASS DEFINITIONS LOOKUP RESULT %%%
  ctxclds_opt {{ tex \overline{ctxcld}_{opt} }} :: ctxclds_opt_ ::=
           {{ com class def.'s lookup result ($\overline{\mathit{ctxcld} }$ option) }} {{ isa ctxclds option }}
   |  null     :: M :: none  {{ com none }} {{ isa None }}
   |  ctxclds  :: M :: some  {{ com some }} {{ isa Some ([[ctxclds]]) }}

%%% TYPES %%%
  tys {{ tex \overline{\tau} }} :: tys_ ::=  {{ com types ($\tau$ list) }} {{ isa ty list }}
   |  ty1 .. tyk  :: M :: def  {{ com def. }} {{ isa [[ty1..tyk]] }}

%%% METHOD TYPE %%%
  mty {{ tex \pi }} :: mty_ ::=  {{ com method type }}
   |  tys -> ty              ::   :: def    {{ com def. }}

%%% TYPE ENVIRONMENT %%%
  G {{ tex \Gamma }} {{ isa \<Gamma> }} :: g_ ::=
           {{ com type environment ($\mathit{x} \rightharpoonup \tau$) }}
           {{ isa x \<rightharpoonup> ty }}
   |  [ x1 |-> ty1  ..  xk |-> tyk ]  :: M :: many
          {{ com type mappings }}
          {{ isa (map_of ([[x1 ty1 .. xk tyk]])) }}
   |  G [ x |-> ty ]                  :: M :: add
          {{ com [[G]] with [[x]]$[[|->]]$[[ty]] }}
          {{ isa ([[G]] ([[x]] [[|->]] [[ty]])) }}

%%% VARIABLE TO VARIABLE MAPPING %%%
  T {{ tex \theta }} :: t_ ::=
           {{ com variable mapping ($\mathit{x} \rightharpoonup \mathit{x}$) }}
           {{ isa x \<rightharpoonup> x }}
   |  [ x1 |-> y1  ..  xk |-> yk ]  :: M :: many
          {{ com variable mappings }}
          {{ isa (map_of ([[x1 y1 .. xk yk]])) }}
   |  T [ x |-> y ]                 :: M :: add
          {{ com [[T]] with [[x]]$[[|->]]$[[y]] }}
          {{ isa ([[T]] ([[x]] [[|->]] [[y]])) }}

%%% VALUE %%%
  Val {{ isa v }} , v , w :: v_ ::=  {{ com value }}
   |  null  :: :: null  {{ com null value }}
   |  oid   :: :: oid   {{ com object identifier }}

%%% VALUE OPTION %%%
  v_opt {{ tex { v_{opt} } }} :: v_opt_ ::=
           {{ com result of value lookup ($\mathit{v}$ option) }} {{ isa v option }}
   |  v              :: M :: some   {{ com some }} {{ isa Some [[v]] }}
   |  L ( x )        :: M :: varstate
           {{ com lookup value of local variable }}
           {{ isa [[L]] [[x]] }}
   |  H ( oid , f )  :: M :: heap
           {{ com lookup value of field }}
           {{ isa (case [[H]] [[oid]] of
                     None \<Rightarrow> None
                   | Some tyfs \<Rightarrow> (snd tyfs) [[f]]) }}

%%% VARIABLE STATE %%%
  L  :: l_ ::=  {{ com variable state ($\mathit{x} \rightharpoonup \mathit{v}$) }}
                {{ isa x \<rightharpoonup> v }}
   |  []                              :: M :: empty
          {{ com empty variable state }} {{ isa empty }}
   |  L [ x |-> v ]                   :: M :: add_one
          {{ com [[L]] with [[x]]$[[|->]]$[[v]] }}
          {{ isa ([[L]] ([[x]] [[|->]] [[v]])) }}
   |  L [ x1 |-> v1  ..  xk |-> vk ]  :: M :: add_many
          {{ com [[L]] with many mappings }}
          {{ isa ([[L]] ++ (map_of ([[x1 v1 .. xk vk]]))) }}

%%% HEAP %%%
  H  :: h_ ::=
      {{ com heap ($\mathit{oid} \rightharpoonup (\tau \times (\mathit{f} \rightharpoonup \mathit{v}))$) }}
      {{ isa oid \<rightharpoonup> (ty \<times> (f \<rightharpoonup> v)) }}
   |  []                                             :: M :: empty
          {{ com empty heap }} {{ isa empty }}
   |  H [ oid |-> ( ty , f1 |-> v1 .. fk |-> vk ) ]  :: M :: new
          {{ com [[H]] with new [[oid]] of type [[ty]] }}
          {{ isa (([[H]] ([[oid]] [[|->]] ([[ty]],
                          map_of [[f1 v1..fk vk]] )))::H) }}
   |  H [ ( oid , f ) |-> v ]                        :: M :: update
          {{ com [[H]] with ([[oid]],[[f]])$[[|->]]$[[v]] }}
          {{ isa (case [[H]] [[oid]] of
                None \<Rightarrow> arbitrary
              | Some tyfs \<Rightarrow>
                   (([[H]] ([[oid]] [[|->]]
                       (fst tyfs, snd tyfs ([[f]] [[|->]] [[v]]))))::H)) }}

%%% CONFIGURATION %%%
  config :: config_ ::=  {{ com configuration }}
   |  ( P , L , H , </ sk // k /> )          :: :: normal
          {{ com normal configuration }}
   |  ( P , L , H , Exception )  :: :: ex
          {{ com exception occurred }}

%%% EXCEPTION %%%
  Exception :: ex_ ::=  {{ com exception }}
   |  NPE   :: :: npe   {{ com null-pointer exception }}

%%% FORMULA %%%
  formula :: formula_ ::=  {{ com formulas }}
   |  judgement                       ::   :: judgement
          {{ com judgement }}
   |  formula1 ... formulak           :: M :: dots
          {{ com conjunction of formulas }}
          {{ isa list_all (\<lambda>f. f) [[formula1...formulak]] }}
   |  not formula                     :: M :: not
          {{ com negated formula }} {{ tex \neg [[formula]] }}
          {{ isa \<not> ([[formula]]) }}
   |  ( formula )                     :: M :: brackets
          {{ com bracketed }}
          {{ tex \left([[formula]]\right) }} {{ isa ([[formula]]) }}
   |  [multiline] formula             :: M :: multiline
           {{ com allow \LaTeX{} output in multiple lines }}
           {{ isa [[formula]] }}
           {{ tex \begin{array}{l}[[formula]]\end{array} }} 
   |  [newline] formula               :: M :: formula_n
           {{ com add a newline to the \LaTeX{} output }}
           {{ isa [[formula]] }} {{ tex \\[[formula]] }} 
   |  [tab] formula                   :: M :: formula_t
           {{ com add a tab to the \LaTeX{} output }}
           {{ isa [[formula]] }} {{ tex \qquad[[formula]] }} 
   |  formula \/ formula'             :: M :: or
          {{ com or }}
          {{ isa [[formula]] \<or> [[formula']] }}
   |  formula /\ formula'             :: M :: and
          {{ com and }}
          {{ isa [[formula]] \<and> [[formula']] }}
   |  formula --> formula'            :: M :: implies
          {{ com implies }}
          {{ isa [[formula]] \<longrightarrow> [[formula']] }}
   |  nn > nn'                        :: M :: nngtnn
           {{ com [[nn]] is greater than [[nn']] }}
           {{ isa [[nn]] > [[nn']] }}
   |  nn =< nn'                       :: M :: nnlenn
           {{ com [[nn]] is less than or equal to [[nn']] }}
           {{ isa [[nn]] \<le> [[nn']] }}
   |  T ( x ) = x'                    :: M :: trx
          {{ com translate [[x]] in [[T]] }}
          {{ isa (case [[T]] [[x]] of None \<Rightarrow> [[x]]
                                    | Some x' \<Rightarrow> x') = [[x']] }}
   |  T ( var ) = var'                :: M :: trvar
          {{ com translate [[var]] in [[T]] }}
          {{ isa (case [[T]] (x_var [[var]]) of
                    None \<Rightarrow> [[var]] | Some x' \<Rightarrow>
                 (case x' of x_this \<Rightarrow> [[var]]
                           | x_var var' \<Rightarrow> var')) = [[var']] }}
   |  X = X'                          :: M :: Xali
           {{ com variables alias }} {{ isa [[X]] = [[X']] }}
   |  fs = fs'                        :: M :: fsali
           {{ com fields alias }} {{ isa [[fs]] = [[fs']] }}
   |  fds = fds'                      :: M :: fdsali
           {{ com field declarations alias }} {{ isa [[fds]] = [[fds']] }}
   |  meths = meths'                  :: M :: methsali
           {{ com method names alias }} {{ isa [[meths]] = [[meths']] }}
   |  cld = cld'                      :: M :: cldali
           {{ com class def.~alias }} {{ isa [[cld]] = [[cld']] }}
   |  clds = clds'                    :: M :: cldsali
           {{ com class def.'s alias }} {{ isa [[clds]] = [[clds']] }}
   |  P = P'                          :: M :: psali
           {{ com program alias }} {{ isa [[P]] = [[P']] }}
   |  ctxclds = ctxclds'              :: M :: ctxctldsali
           {{ com class def.'s with context alias }}
           {{ isa [[ctxclds]] = [[ctxclds']] }}
   |  meth_def = meth_def'            :: M :: methdefali
           {{ com method def.~alias }}
           {{ isa [[meth_def]] = [[meth_def']] }}
   |  tys = tys'                      :: M :: tysali
           {{ com types alias }} {{ isa [[tys]] = [[tys']] }}
   |  ty_opt = ty_opt'                :: M :: typotali
           {{ com type option alias }} {{ isa [[ty_opt]] = [[ty_opt']] }}
   |  mty = mty'                      :: M :: mtyali
           {{ com method type alias }} {{ isa [[mty]] = [[mty']] }}
   |  T = T'                          :: M :: thetaali
           {{ com variable mapping alias }} {{ isa [[T]] = [[T']] }}
   |  L = L'                          :: M :: varstateali
           {{ com variable state alias }} {{ isa [[L]] = [[L']] }}
   |  H = H'                          :: M :: heapali
           {{ com heap alias }} {{ isa [[H]] = [[H']] }}
   |  G = G'                          :: M :: tenvali
          {{ com type environment alias }} {{ isa [[G]] = [[G']] }}
   |  v_opt = v                       :: M :: valali
          {{ com value lookup alias }} {{ isa [[v_opt]] = Some [[v]] }}
   |  v = w                           :: M :: valeq
          {{ com value equality }} {{ isa [[v]] = [[w]] }}
   |  cl != cl'                       :: M :: clali
           {{ com class name inequality }}
           {{ isa [[cl]] \<noteq> [[cl']] }}
   |  f != f'                         :: M :: fneq
           {{ com field name inequality }} {{ isa [[f]] \<noteq> [[f']] }}
   |  meth != meth'                   :: M :: methneq
           {{ com method name inequality }} {{ isa [[meth]] \<noteq> [[meth']] }}
   |  v != w                          :: M :: valneq
          {{ com value inequality }} {{ isa [[v]] \<noteq> [[w]] }}
   |  ty != ty'                       :: M :: tyneq
          {{ com type inequality }} {{ isa [[ty]] \<noteq> [[ty']] }}
   |  </ xk // k /> bot dom ( L )     :: M :: varsdom_disjoint
          {{ com disjoint variables and domain }}
          {{ isa (set [[ </ xk // k /> ]]) Int (dom [[L]]) = {} }}
   |  fs bot fs'                      :: M :: fields_disjoint
          {{ com disjoint field sets }}
          {{ isa (set [[fs]]) \<inter> (set [[fs']]) = {} }}
   |  distinct ( </ vark // k /> )    :: M :: distinct_var
          {{ com variable names distinct }}
          {{ isa distinct ([[ </ vark // k /> ]]) }}
   |  distinct ( </ fk // k /> )      :: M :: distinct_f
          {{ com field names are distinct }}
          {{ isa distinct ([[ </ fk // k /> ]]) }}
   |  distinct ( </ methk // k /> )   :: M :: distinct_meth
          {{ com method names are distinct }}
          {{ isa distinct ([[ </ methk // k /> ]]) }}
   |  meth in meths                   :: M :: nomethod
          {{ com [[meth]] in [[meths]] }}
          {{ isa [[meth]] \<in> set [[meths]] }}
   |  finite ( dom ( L ) )            :: M :: finitedomvarstate
          {{ com finite domain of [[L]] }}
          {{ isa finite (dom ([[L]])) }}
   |  finite ( dom ( H ) )            :: M :: finitedomheap
          {{ com finite domain of [[H]] }}
          {{ isa finite (dom ([[H]])) }}
   |  oid notin dom ( H )             :: M :: freshobject
          {{ com [[oid]] not in domain of [[H]] }}
          {{ isa [[oid]] \<notin> dom [[H]] }}
   |  x notin dom ( L )               :: M :: freshx_L
          {{ com [[x]] not in domain of [[L]] }}
          {{ isa [[x]] \<notin> dom [[L]] }}
   |  x notin </ xk // k />           :: M :: freshx_list
          {{ com [[x]] not in list }}
          {{ isa [[x]] \<notin> set [[ </ xk // k /> ]] }}
   |  EX ty . formula                 :: M :: ex_ty
          {{ com there exists a [[ty]] }}
          {{ isa \<exists> [[ty]] . [[formula]] }}
   |  EX fs . formula                 :: M :: ex_fs
          {{ com there exist [[fs]] }}
          {{ isa (\<exists> [[fs]] . [[formula]]) }}
   |  EX ctx cld . formula            :: M :: ex_ctxcld
          {{ com there exists [[ctx]] and [[cld]] }}
          {{ isa (\<exists> [[ctx]] [[cld]] . [[formula]]) }}
   |  EX nn . formula                 :: M :: ex_nn
           {{ com there exists [[nn]] }}
           {{ isa (\<exists> [[nn]] . [[formula]]) }}
   |  ALL x in dom ( G ) . formula    :: M :: all_vars
          {{ com for all variables in domain of [[G]] }}
          {{ isa \<forall> [[x]] \<in> dom [[G]]. [[formula]] }}
   |  ALL f in fs . formula           :: M :: all_f
          {{ com for all fields in [[fs]] }}
          {{ isa (\<forall> [[f]] \<in> set [[fs]]. [[formula]]) }}
   |  ALL oid in dom ( H ) . formula  :: M :: all_obj
          {{ com for all objects in domain of [[H]] }}
          {{ isa \<forall> [[oid]] \<in> dom [[H]]. [[formula]] }}

parsing
nn_plus left nn_plus
formula_and left formula_and
formula_multiline <= formula_and
formula_multiline <= formula_or

%%% METHODNAME %%%
defns
method_name_def :: '' ::=

defn
method_name ( meth_def ) = meth :: :: method_name :: ''
           {{ com extract the method name from a method definition }} by

------------------------------------------- :: method_name
method_name(cl meth(vds) {meth_body}) = meth


%%% FIND TYPE %%%
defns
find_type_def :: '' ::=

defn
find_type ( P , ctx , cl ) = ty_opt :: :: find_type :: ft_
           {{ com type lookup }} by

----------------------------------- :: obj
find_type (P, ctx, Object) = Object

find_cld(P, ctx, fqn) = null
------------------------------ :: null
find_type (P, ctx, fqn) = null

%%% PATH LENGTH %%%
defns
path_length_def :: '' ::=

defn
( P , ctx , cl , nn ) in path_length :: :: path_length :: pl_
           {{ com get the length of the inheritance path for [[cl]] }} by

---------------------------------- :: obj
(P, ctx, Object, 0) in path_length

find_cld(P, ctx, fqn) = (ctx', cld)
superclass_name(cld) = cl
(P, ctx', cl, nn) in path_length
------------------------------------ :: fqn
(P, ctx, fqn, nn + 1) in path_length

%%% FIND PATH REC %%%
defns
find_path_rec_def :: '' ::=

defn
find_path_rec ( P , ctx , cl , ctxclds ) = ctxclds_opt :: :: find_path_rec :: fpr_
           {{ com class path lookup (recursive part) }} by

------------------------------------------------ :: obj
find_path_rec(P, ctx, Object, ctxclds) = ctxclds

(not acyclic_clds P) \/ find_cld(P, ctx, fqn) = null
---------------------------------------------------- :: null
find_path_rec(P, ctx, fqn, ctxclds) = null

acyclic_clds P /\ find_cld(P, ctx, fqn) = (ctx', cld)
superclass_name(cld) = cl
find_path_rec(P, ctx', cl,  ctxclds @ [(ctx', cld)]) = ctxclds_opt
------------------------------------------------------------------ :: fqn
find_path_rec(P, ctx, fqn, ctxclds) = ctxclds_opt

%%% FIND PATH %%%
defns
find_path_def :: '' ::=

defn
find_path ( P , ctx , cl ) = ctxclds_opt :: :: find_path :: fp_
           {{ com class path lookup with a class name }} by

find_path_rec(P, ctx, cl, []) = ctxclds_opt
------------------------------------------- :: def
find_path(P, ctx, cl) = ctxclds_opt

%%% FIND PATH (TY) %%%
defns
find_path_ty_def :: '' ::=

defn
find_path ( P , ty ) = ctxclds_opt :: :: find_path_ty :: fpty_
           {{ com class path lookup with a type }} by

------------------------- :: obj
find_path(P, Object) = []


%%% FIELDS IN PATH %%%
defns
fields_in_path_def :: '' ::=

defn
fields_in_path ( ctxclds ) = fs :: :: fields_in_path :: fip_
           {{ com fields lookup in a class path }} by

----------------------- :: empty
fields_in_path([]) = []

class_fields(cld) = </clj fj;//j/>
fields_in_path(ctxcld2..ctxcldk) = fs
fs' = </fj//j/>; fs
------------------------------------------------- :: cons
fields_in_path((ctx, cld) ctxcld2..ctxcldk) = fs'


%%% FIELDS %%%
defns
fields_def :: '' ::=

defn
fields ( P , ty ) = fs_opt :: :: fields :: fields_
           {{ com fields lookup in type [[ty]] }} by

find_path(P, ty) = null
----------------------- :: none
fields(P, ty) = null

find_path(P, ty) = ctxclds
fields_in_path(ctxclds) = fs
---------------------------- :: some
fields(P, ty) = fs


%%% METHODS IN PATH %%%
defns
methods_in_path_def :: '' ::=

defn
methods_in_path ( clds ) = meths :: :: methods_in_path :: mip_
           {{ com method names lookup in a path }} by

------------------------ :: empty
methods_in_path([]) = []

class_methods(cld) = </meth_defl//l/>
</meth_defl = cll methl(vdsl) {meth_bodyl}//l/>
methods_in_path(cld2..cldk) = meths'
meths = </methl//l/>; meths'
----------------------------------------------- :: cons
methods_in_path(cld cld2..cldk) = meths


%%% METHODS %%%
defns
methods_def :: '' ::=

defn
methods ( P , ty ) = meths :: :: methods :: methods_
           {{ com method names lookup in a type }} by

find_path(P, ty) = </(ctxk, cldk)//k/>
methods_in_path(</cldk//k/>) = meths
-------------------------------------- :: methods
methods(P, ty) = meths

%%% FTYPE IN FDS %%%
defns
ftype_in_fds_def :: '' ::=

defn
ftype_in_fds ( P , ctx , fds , f ) = ty_opt_bot :: :: ftype_in_fds :: ftif_
           {{ com field type lookup in a list }} by

----------------------------------- :: empty
ftype_in_fds (P, ctx, [], f) = null

find_type(P, ctx, cl) = null
---------------------------------------------- :: cons_bot
ftype_in_fds (P, ctx, cl f; fd2..fdk, f) = bot

find_type(P, ctx, cl) = ty
--------------------------------------------- :: cons_true
ftype_in_fds (P, ctx, cl f; fd2..fdk, f) = ty

f != f'
ftype_in_fds(P, ctx, fd2..fdk, f') = ty_opt_bot
------------------------------------------------------ :: cons_false
ftype_in_fds (P, ctx, cl f; fd2..fdk, f') = ty_opt_bot


%%% FTYPE IN PATH %%%
defns
ftype_in_path_def :: '' ::=

defn
ftype_in_path ( P , ctxclds , f ) = ty_opt :: :: ftype_in_path :: ftip_
           {{ com field type lookup in a path }} by

------------------------------ :: empty
ftype_in_path(P, [], f) = null

class_fields(cld) = fds
ftype_in_fds(P, ctx, fds, f) = bot
------------------------------------------------------- :: cons_bot
ftype_in_path(P, (ctx, cld) ctxcld2..ctxcldk, f) = null

class_fields(cld) = fds
ftype_in_fds(P, ctx, fds, f) = ty
----------------------------------------------------- :: cons_true
ftype_in_path(P, (ctx, cld) ctxcld2..ctxcldk, f) = ty

class_fields(cld) = fds
ftype_in_fds(P, ctx, fds, f) = null
ftype_in_path(P, ctxcld2..ctxcldk, f) = ty_opt
--------------------------------------------------------- :: cons_false
ftype_in_path(P, (ctx, cld) ctxcld2..ctxcldk, f) = ty_opt


%%% FTYPE %%%
defns
ftype_def :: '' ::=

defn
ftype ( P , ty , f ) = ty' :: :: ftype :: ''
           {{ com field type lookup }} by

find_path(P, ty) = ctxclds
ftype_in_path(P, ctxclds, f) = ty'
---------------------------------- :: ftype
ftype(P, ty, f) = ty'


%%% FIND METH_DEF IN LIST %%%
defns
find_meth_def_in_list_def :: '' ::=

defn
find_meth_def_in_list ( meth_defs , meth ) = meth_def_opt :: :: find_meth_def_in_list :: fmdil_
           {{ com meth.~def.~lookup (list) }} by

-------------------------------------- :: empty
find_meth_def_in_list([], meth) = null

meth_def = cl meth(vds) {meth_body}
--------------------------------------------------------------------- :: cons_true
find_meth_def_in_list(meth_def meth_def2..meth_defk, meth) = meth_def

meth_def = cl meth'(vds) {meth_body}
meth != meth'
find_meth_def_in_list(meth_def2..meth_defk, meth) = meth_def_opt
------------------------------------------------------------------------- :: cons_false
find_meth_def_in_list(meth_def meth_def2..meth_defk, meth) = meth_def_opt


%%% FIND METH_DEF IN PATH %%%
defns
find_meth_def_in_path_def :: '' ::=

defn
find_meth_def_in_path ( ctxclds , meth ) = ctxmeth_def_opt :: :: find_meth_def_in_path :: fmdip_
           {{ com meth.~def.~lookup (path) }} by

-------------------------------------- :: empty
find_meth_def_in_path([], meth) = null

class_methods(cld) = meth_defs
find_meth_def_in_list(meth_defs, meth) = meth_def
--------------------------------------------------------------------- :: cons_true
find_meth_def_in_path((ctx, cld) ctxcld2..ctxcldk, meth) = (ctx, meth_def)

class_methods(cld) = meth_defs
find_meth_def_in_list(meth_defs, meth) = null
find_meth_def_in_path(ctxcld2..ctxcldk, meth) = ctxmeth_def_opt
-------------------------------------------------------------------- :: cons_false
find_meth_def_in_path((ctx, cld) ctxcld2..ctxcldk, meth) = ctxmeth_def_opt


%%% FIND METH_DEF %%%
defns
find_meth_def_def :: '' ::=

defn
find_meth_def ( P , ty , meth ) = ctxmeth_def_opt :: :: find_meth_def :: fmd_
           {{ com method def.~lookup in a type }} by

find_path(P, ty) = null
--------------------------------- :: null
find_meth_def(P, ty, meth) = null

find_path(P, ty) = ctxclds
find_meth_def_in_path(ctxclds, meth) = ctxmeth_def_opt
------------------------------------------------------ :: opt
find_meth_def(P, ty, meth) = ctxmeth_def_opt



%%% MTYPE %%%
defns
mtype_def :: '' ::=

defn
mtype ( P , ty , meth ) = mty :: :: mtype :: ''
           {{ com method type lookup }} by

find_meth_def(P, ty, meth) = (ctx, meth_def)
meth_def = cl meth(</clk vark//k/>) {meth_body}
find_type(P, ctx, cl) = ty'
</find_type(P, ctx, clk) = tyk//k/>
mty = </tyk//k/> -> ty'
----------------------------------------------- :: mtype
mtype(P, ty, meth) = mty

%%% STY_ONE %%%
defns
sty_one_def :: sty_ ::=

defn
P |- ty < ty' :: :: one :: '' {{ com subtyping }} by

find_path(P, ty) = ctxclds
-------------------------- :: obj
P |- ty < Object

%%% STY_MANY %%%
defns
sty_many_def :: sty_ ::=

defn
P |- tys < tys' :: :: many :: '' {{ com normal, multiple subtyping }} by

tys = </tyk//k/>
tys' = </tyk'//k/>
</:sty_one: P |- tyk < tyk'//k/>
--------------------------------- :: many
P |- tys < tys'

%%% STY_OPTION %%%
defns
sty_option_def :: sty_ ::=

defn
P |- ty_opt < ty_opt' :: :: option :: '' {{ com option subtyping }} by

ty_opt = ty
ty_opt' = ty'
:sty_one: P |- ty < ty'
------------------------ :: option
P |- ty_opt < ty_opt'




%%% WELL-FORMEDNESS %%%
defns
well_formedness :: wf_ ::=


defn
P , H |- v_opt < ty_opt :: :: object :: '' {{ com well-formed value }} by

ty_opt = ty
--------------------- :: null
P,H |- null < ty_opt

P |- H(oid) < ty_opt
--------------------- :: object
P,H |- oid < ty_opt


defn
P , G , H |- L :: :: varstate :: '' {{ com well-formed variable state }} by

finite(dom(L))
ALL x in dom(G). P,H |- L(x) < G(x)
----------------------------------- :: varstate
P,G,H |- L


defn
P |- H :: :: heap :: '' {{ com well-formed heap }} by

finite(dom(H))
ALL oid in dom(H). ( [multiline] EX ty. :formula_and: H(oid) = ty /\ EX fs . :formula_and: fields(P, ty) = fs /\ [newline] [tab] ALL f in fs. EX ty'. ( [multiline] :formula_and: ftype(P, ty, f) = ty' /\ [newline] P,H |- H(oid, f) < ty' ) )
-------------------------------------------------------------- :: heap
P |- H

defn
G |- config :: :: config :: '' {{ com well-formed configuration }} by

|- P
P |- H
P,G,H |- L
------------------------- :: all_ex
G |- (P, L, H, Exception)

|- P
P |- H
P,G,H |- L
</P,G |- sk//k/>
------------------------- :: all
G |- (P, L, H, </sk//k/>)


defn
P , G |- s ::  :: stmt :: '' {{ com well-formed statement }} by

</P,G |- sk//k/>
------------------ :: block
P,G |- {</sk//k/>}

P |- G(x) < G(var)
------------------- :: var_assign
P,G |- var = x;

G(x) = ty
ftype(P, ty, f) = ty'
P |- ty' < G(var)
------------------------------- :: field_read
P,G |- var = x.f;

G(x) = ty
ftype(P, ty, f) = ty'
P |- G(y) < ty'
----------------------------- :: field_write
P,G |- x.f = y;

P |- G(x) < G(y) \/ P |- G(y) < G(x)
:wf_stmt: P,G |- s1
:wf_stmt: P,G |- s2
-------------------------------------- :: if
P,G |- if (x == y) s1 else s2

find_type(P, ctx, cl) = ty
P |- ty < G(var)
-------------------------- :: new
P,G |- var = new ctx cl();

Y = </yk//k/>
G(x) = ty
mtype(P, ty, meth) = </tyk//k/> -> ty'
</P |- G(yk) < tyk//k/>
P |- ty' < G(var)
--------------------------------------- :: mcall
P,G |- var = x.meth(Y);


defn
P |- ty meth_def :: :: meth :: ''
           {{ com well-formed method in [[ty]] }} 
           {{ tex [[P]][[|-]]_{[[ty]]} [[meth_def]] }} by

distinct(</vark//k/>)
</find_type(P, ctx, clk) = tyk//k/>
G =  :g_add: [</vark |-> tyk//k/>] [this|->ctx.dcl]
</P,G |- sl//l/>
find_type(P, ctx, cl) = ty
P |- G(y) < ty
---------------------------------------------------------- :: method
P |-ctx.dcl cl meth(</clk vark//k/>) {</sl//l/> return y;}

>>
NOTE: Below, the overriding method must have the same type, as in Java;
      however, in theory, it could have its subtype.
<<

defn
P |- ctx ( dcl , cl , fds , meth_defs ) :: :: class_common :: ''
           {{ com well-formed class in [[ctx]] (generic rule) }}
           {{ tex [[P]][[|-]]_{[[ctx]]}([[dcl]],[[cl]],[[fds]],[[meth_defs]]) }} by

find_type(P, ctx, cl) = ty
ctx.dcl != ty
distinct (</fj//j/>)
fields(P, ty) = fs
</fj//j/> bot fs
</find_type(P, ctx, clj) = tyj//j/>
</P |-ctx.dcl meth_defk//k/>
</method_name(meth_defk) = methk//k/>
distinct (</methk//k/>)
methods(P, ty) = </methl'//l/>
</mtype(P, ctx.dcl, methl') = mtyl//l/>
</mtype(P, ty, methl') = mtyl'//l/>
</methl' in </methk//k/> --> mtyl = mtyl' //l/>
--------------------------------------------------- :: class_common
P |-ctx (dcl, cl, </clj fj;//j/>, </meth_defk//k/>)


%%% VARIABLE TRANSLATION %%%
defns
var_trans :: '' ::=

defn
T |- s ~> s'  :: :: tr_s :: tr_s_
           {{ com variable translation for a statement }} by

</T |- sk ~> sk'//k/>
-------------------------------- :: block
T |- {</sk//k/>} ~> {</sk'//k/>}

:formula_trvar: T(var) = var'
T(x) = x'
----------------------------- :: var_assign
T |- var = x; ~> var' = x';

:formula_trvar:  T(var) = var'
T(x) = x'
------------------------------- :: field_read
T |- var = x.f; ~> var' = x'.f;

T(x) = x'
T(y) = y'
--------------------------- :: field_write
T |- x.f = y; ~> x'.f = y';

T(x) = x'
T(y) = y'
T |- s1 ~> s1'
T |- s2 ~> s2'
--------------------------------------------------------- :: if
T |- if (x == y) s1 else s2 ~> if (x' == y') s1' else s2'

:formula_trvar: T(var) = var'
------------------------------------------------ :: new
T |- var = new ctx cl(); ~> var' = new ctx cl();

:formula_trvar: T(var) = var'
T(x) = x'
</T(yk) = yk'//k/>
------------------------------------------------------------ :: mcall
T |- var = x.meth(</yk//k/>); ~> var' = x'.meth(</yk'//k/>);


%%% REDUCTION %%%
defns
reduction :: r_ ::=

defn
config --> config' ::  :: stmt :: '' {{ com reduction of a statement }} by

-------------------------------------------------------------------- :: block
(P, L, H, {</sk//k/>} </sl'//l/>) --> (P, L, H, </sk//k/> </sl'//l/>)

L(x) = v
-------------------------------------------------------------------- :: var_assign
(P, L, H, var = x; </sl//l/>) --> (P, :l_add_one:L[var |-> v], H, </sl//l/>)

L(x) = null
-------------------------------------------------- :: field_read_npe
(P, L, H, var = x.f; </sl//l/>) --> (P, L, H, NPE)

L(x) = oid
H(oid, f) = v
-------------------------------------------------------------------- :: field_read
(P, L, H, var = x.f; </sl//l/>) --> (P, :l_add_one:L[var |-> v], H, </sl//l/>)

L(x) = null
------------------------------------------------ :: field_write_npe
(P, L, H, x.f = y; </sl//l/>) --> (P, L, H, NPE)

L(x) = oid
L(y) = v
-------------------------------------------------------------------- :: field_write
(P, L, H, x.f = y; </sl//l/>) --> (P, L, H[(oid, f) |-> v], </sl//l/>)

L(x) = v
L(y) = w
:formula_valeq: v = w
------------------------------------------------------------------------- :: if_true
(P, L, H, if (x == y) s1 else s2 </sl'//l/>) --> (P, L, H, s1 </sl'//l/>)

L(x) = v
L(y) = w
v != w
------------------------------------------------------------------------- :: if_false
(P, L, H, if (x == y) s1 else s2 </sl'//l/>) --> (P, L, H, s2 </sl'//l/>)

find_type(P, ctx, cl) = ty
fields(P, ty) = </fk//k/>
oid notin dom(H)
H' = H [oid |-> (ty, </fk |-> null//k/>)]
------------------------------------------------------------------------- :: new
(P, L, H, var = new ctx cl(); </sl//l/>) --> (P, :l_add_one:L[var |-> oid], H', </sl//l/>)

L(x) = null
---------------------------------------------------------------- :: mcall_npe
(P, L, H, var = x.meth(</yk//k/>); </sl//l/>) --> (P, L, H, NPE)

L(x) = oid
H(oid) = ty
find_meth_def(P, ty, meth) = (ctx, cl meth(</clk vark//k/>) {</sj'//j/> return y;})
</vark'//k/> bot dom(L)
distinct(</vark'//k/>)
x' notin dom(L)
x' notin </vark'//k/>
</L(yk) = vk//k/>
L' = :l_add_one: L [</vark' |-> vk//k/>] [x' |-> oid]
T = :t_add:[ </vark |-> vark'//k/>] [this |-> x']
</T |- sj' ~> sj''//j/>
T(y) = y'
------------------------------------------------------------------------- :: mcall
(P, L, H, var = x.meth(</yk//k/>); </sl//l/>) --> (P, L', H, </sj''//j/> var = y'; </sl//l/>)
